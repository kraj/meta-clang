FILESEXTRAPATHS =. "${FILE_DIRNAME}/clang:"

LIC_FILES_CHKSUM = "file://llvm/LICENSE.TXT;md5=${LLVMMD5SUM} \
                    file://clang/LICENSE.TXT;md5=${CLANGMD5SUM} \
"
LICENSE = "Apache-2.0-with-LLVM-exception"

# Snapshot
#RELEASE ?= "7ba7d8e2f7b6445b60679da826210cdde29eaf8b"
#BASEURI ?= "https://api.github.com/repos/llvm/llvm-project/tarball/${RELEASE};downloadfilename=llvm-${PV}-${RELEASE}.tar.gz"
#SOURCEDIR ?= "llvm-llvm-project-${@'${RELEASE}'[0:7]}"
#SRC_URI[sha256sum] = "cca3fa1c6efb72bd8bc22b926a2c5551e6b8560d95828452bc62b9140cb87a90"

# GA Release
RELEASE ?= "${PV}"
BASEURI ?= "${LLVM_HTTP}/llvm-project/releases/download/llvmorg-${PV}/llvm-project-${PV}.src.tar.xz"
SOURCEDIR ?= "llvm-project-${PV}.src"
SRC_URI[sha256sum] = "3666f01fc52d8a0b0da83e107d74f208f001717824be0b80007f529453aa1e19"

SRC_URI = "\
    ${BASEURI} \
    file://0001-libcxxabi-Find-libunwind-headers-when-LIBCXXABI_LIBU.patch \
    file://0002-compiler-rt-support-a-new-embedded-linux-target.patch \
    file://0003-compiler-rt-Simplify-cross-compilation.-Don-t-use-na.patch \
    file://0004-llvm-TargetLibraryInfo-Undefine-libc-functions-if-th.patch \
    file://0005-llvm-allow-env-override-of-exe-and-libdir-path.patch \
    file://0006-clang-driver-Check-sysroot-for-ldso-path.patch \
    file://0007-clang-Driver-tools.cpp-Add-lssp_nonshared-on-musl.patch \
    file://0008-clang-Prepend-trailing-to-sysroot.patch \
    file://0009-clang-Look-inside-the-target-sysroot-for-compiler-ru.patch \
    file://0010-clang-Define-releative-gcc-installation-dir.patch \
    file://0011-clang-Add-lpthread-and-ldl-along-with-lunwind-for-st.patch \
    file://0012-Pass-PYTHON_EXECUTABLE-when-cross-compiling-for-nati.patch \
    file://0013-Check-for-atomic-double-intrinsics.patch \
    file://0014-libcxx-Add-compiler-runtime-library-to-link-step-for.patch \
    file://0015-cmake-Fix-configure-for-packages-using-find_package.patch \
    file://0016-clang-Fix-resource-dir-location-for-cross-toolchains.patch \
    file://0017-clang-driver-Add-dyld-prefix-when-checking-sysroot-f.patch \
    file://0018-clang-Use-python3-in-python-scripts.patch \
    file://0019-For-x86_64-set-Yocto-based-GCC-install-search-path.patch \
    file://0020-llvm-Insert-anchor-for-adding-OE-distro-vendor-names.patch \
    file://0021-compiler-rt-Do-not-use-backtrace-APIs-on-non-glibc-l.patch \
    file://0022-clang-Fix-x86-triple-for-non-debian-multiarch-linux-.patch \
    file://0023-libunwind-Added-unw_backtrace-method.patch \
    file://0024-clang-Do-not-use-install-relative-libc-headers.patch \
    file://0025-Fix-lib-paths-for-OpenEmbedded-Host.patch \
    file://0026-Correct-library-search-path-for-OpenEmbedded-Host.patch \
    file://0027-lldb-Link-with-libatomic-on-x86.patch \
    file://0028-compiler-rt-Enable-__int128-for-ppc32.patch \
    file://0029-llvm-Do-not-use-cmake-infra-to-detect-libzstd.patch \
    file://0030-compiler-rt-Fix-stat-struct-s-size-for-O32-ABI.patch \
    file://0031-compiler-rt-Undef-_TIME_BITS-along-with-_FILE_OFFSET.patch \
    file://0032-ToolChains-Gnu.cpp-ARMLibDirs-search-also-in-lib32.patch \
    file://0033-clang-llvm-Add-OE-specific-ABI-triple-for-N32-ABI.patch \
    file://0034-llvm-Add-libunwind.pc.in-and-llvm-config-scripts.patch \
    file://0035-scan-build-py-respect-LLVM_LIBDIR_SUFFIX-like-other-.patch \
"
# Fallback to no-PIE if not set
GCCPIE ??= ""

S = "${TMPDIR}/work-shared/llvm-project-source-${PV}-${PR}/${SOURCEDIR}"
B ?= "${WORKDIR}/llvm-project-source-${PV}/build.${HOST_SYS}.${TARGET_SYS}"

# We need to ensure that for the shared work directory, the do_patch signatures match
# The real WORKDIR location isn't a dependency for the shared workdir.
src_patches[vardepsexclude] = "WORKDIR"
should_apply[vardepsexclude] += "PN"
